package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER, IGUAL_IGUAL, DIFERENTE, MAYOR, MENOR, MENOR_IGUAL, MAYOR_IGUAL, AND, OR, NOT;
nonterminal NodoC3D inicial, expresion, logico;
nonterminal String signo_relacional;

precedence left OR;
precedence left AND;
precedence left NOT;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} logico:log 
                    {: 
                        RESULT = log;
						if(log.getCad() != null){
							ControlC3D.agregarC3D("//Ãšltimo valor: " + log.getCad());
						}else{
							ControlC3D.agregarC3D("//Etiqueta(s) de verdadero: " + log.removeLv() + "\n");
							ControlC3D.agregarC3D("//Etiqueta(s) de falso: " + log.removeLf() + "\n");
						}
                        
                    :};

logico  ::= logico:izq OR{:ControlC3D.agregarC3D(izq.removeLf() + ":\n");:} logico:der
				{:
					RESULT = new NodoC3D();
					RESULT.setLv(izq.getLv() + " " + der.getLv());
					RESULT.setLf(izq.getLf() + " " + der.getLf());
				:}
		| logico:izq AND{:ControlC3D.agregarC3D(izq.removeLv() + ":\n");:} logico:der
				{:
					RESULT = new NodoC3D();
					RESULT.setLv(izq.getLv() + " " + der.getLv());
					RESULT.setLf(izq.getLf() + " " + der.getLf());
				:}
		| NOT logico:log
				{:
					RESULT = new NodoC3D();
					RESULT.setLf(log.removeLv());
					RESULT.setLv(log.removeLf	());
				:}		
		| expresion:izq signo_relacional:rel expresion:der
				{:
					RESULT = new NodoC3D();
					RESULT.setLv(ControlC3D.generarEtiqueta());
					RESULT.setLf(ControlC3D.generarEtiqueta());
					ControlC3D.agregarC3D("if " + izq.getCad() + rel + der.getCad() + " then goto " + RESULT.getLv() + ";\n");
					ControlC3D.agregarC3D("goto " + RESULT.getLf() + ";\n");
				:} 
		| expresion:exp
				{:
					RESULT  = exp;
				:};

signo_relacional ::= IGUAL_IGUAL
					{:
						RESULT = " == ";
					:}
					| DIFERENTE
					{:
						RESULT = " != ";
					:}
					| MAYOR
					{:
						RESULT = " > ";
					:}
					| MENOR
					{:
						RESULT = " < ";
					:}
					| MAYOR_IGUAL
					{:
						RESULT = " >= ";
					:}
					| MENOR_IGUAL
					{:
						RESULT = " <= ";
					:};

expresion   ::= expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   PAR_IZQ logico:exp PAR_DER   
                    {: RESULT = exp; :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;